Program -> Declaration-list $
Declaration-list -> Declaration Declaration-list | EPSILON 
Declaration -> Declaration-initial Declaration-prime
Declaration-initial ->  Type-specifier ID #startNoPush #pid #endNoPush
Declaration-prime -> Fun-declaration-prime | Var-declaration-prime #zeroInitialize
Var-declaration-prime -> ; | [ NUM #pnum ] #declareArray ; 
Fun-declaration-prime -> ( #declareFunction #openScope #setFunctionScopeFlag Params ) Compound-stmt #jumpBack
Type-specifier -> int | void
Params -> int ID #pid Param-prime #popParam Param-list | void
Param-list -> , Param Param-list | EPSILON
Param -> Declaration-initial Param-prime #popParam
Param-prime -> [  ] #arrayParam | EPSILON
Compound-stmt -> { #openScope Declaration-list Statement-list #closeScope }
Statement-list -> Statement Statement-list | EPSILON
Statement -> Expression-stmt | Compound-stmt | Selection-stmt | Iteration-stmt | Return-stmt
Expression-stmt -> Expression #pop ; | break #break ; | ;
Selection-stmt -> if ( Expression ) #save Statement Else-stmt
Else-stmt -> endif #jpfFromSaved | else #saveAndJpfFromLastSave Statement endif #jpFromSaved
Iteration-stmt -> repeat #label Statement until ( Expression ) #until #handleBreaks
Return-stmt -> return Return-stmt-prime #jumpBack
Return-stmt-prime -> ; | Expression #setReturnValue ;
Expression -> Simple-expression-zegond | ID #checkDeclaration #pid #uncheckDeclaration B
B -> = Expression #assign | [ Expression ] #array H | Simple-expression-prime
H -> = Expression #assign | G D C
Simple-expression-zegond -> Additive-expression-zegond C
Simple-expression-prime -> Additive-expression-prime C
C -> Relop Additive-expression #execute | EPSILON
Relop -> < #pushOperation | == #pushOperation
Additive-expression -> Term D
Additive-expression-prime -> Term-prime D
Additive-expression-zegond -> Term-zegond D
D -> Addop Term #execute D | EPSILON
Addop -> + #pushOperation | - #pushOperation
Term -> Factor G
Term-prime -> Factor-prime G
Term-zegond -> Factor-zegond G
G -> * #pushOperation Factor #execute G | EPSILON
Factor -> ( Expression ) | ID #checkDeclaration #pid #uncheckDeclaration Var-call-prime | NUM #pnum
Var-call-prime -> ( #startArgumentList Args #endArgumentList ) #call | Var-prime
Var-prime -> [ Expression ] #array | EPSILON
Factor-prime -> ( #startArgumentList Args #endArgumentList ) #call | EPSILON
Factor-zegond -> ( Expression ) | NUM #pnum
Args -> Arg-list | EPSILON
Arg-list -> Expression Arg-list-prime #addArgumentCount
Arg-list-prime -> , Expression Arg-list-prime #addArgumentCount | EPSILON